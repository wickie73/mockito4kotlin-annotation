ext {
    libGroupId = 'io.github.wickie73'
    libArtifactId = 'mockito4kotlin.annotation'
    libVersion = version
    title = 'Mockito Annotations for Kotlin'
    descriptionPOM = 'Mockito Annotations for Kotlin: a small Kotlin library which supports Annotations for Mockito, Mockito-Kotlin or Mockito4k.'
    licenseURL = 'https://opensource.org/licenses/MIT'
    projectURL = 'https://github.com/wickie73/mockito4kotlin.annotation'
}

ext.sourceManifest = manifest {
    attributes(
        "Bundle-Name": title + " Sources",
        "Bundle-SymbolicName": project.name + ".source",
        "Bundle-Version": version,
        "Bundle-ManifestVersion": "2",
        "Bundle-ActivationPolicy": "lazy",
        "Bundle-License": licenseURL,
        "Created-By": "wickie73",
        "Eclipse-SourceBundle": project.name + ";version=" + '"' + version + '"'
    )
}

jar.manifest {
    attributes(
        "Bundle-Name": title,
        "Bundle-SymbolicName": project.name,
        "Bundle-Version": version,
        "Bundle-RequiredExecutionEnvironment": "JavaSE-1.8",
        "Bundle-ManifestVersion": "2",
        "Bundle-ActivationPolicy": "lazy",
        "Bundle-License": licenseURL,
        "Implementation-Title": title,
        "Implementation-Version": version,
        "Created-By": "wickie73",
        "Specification-Title": title,
        "Specification-Version": version,
        "Specification-Vendor": "wickie73",
        "Export-Package": "io.github.wickie73.mockito4kotlin.annotation",
        "Import-Package": 'com.nhaarman.mockitokotlin2;version="[2.0.0,3)"',
        "Require-Bundle": 'org.mockito;version="[2.24,3)",org.jetbrains.kotlin;version="[1.3.0,1.5)"',
        "Build-Jdk": java_version
    )
}

// javadoc / dokka
dokka {
    outputFormat = "html"
    outputDirectory = "build/dokka"

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/wickie73/mockito4kotlin.annotation/tree/master/src/main/kotlin"
        suffix = "#L"
    }

    jdkVersion = 8
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    manifest = project.manifest {
        from sourceManifest
    }
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from dokka.outputDirectory
}

javadoc.dependsOn dokka

artifacts {
    archives javadocJar, sourcesJar
}

// Signing
apply plugin: 'signing'
signing {
    sign configurations.archives
}


// Deploying
apply plugin: 'maven'

// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // locale
            repositories {
                maven {
                    name = 'myRepo'
                    url = "file://${buildDir}/repo"
                }
            }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            // Add required metadata to POM
            pom.project {
                name title
                packaging 'jar'
                description descriptionPOM
                url projectURL

                groupId libGroupId
                artifactId libArtifactId
                version libVersion

                scm {
                    connection 'scm:git:git://github.com/wickie73/mockito4kotlin.annotation.git'
                    url 'https://github.com/wickie73/mockito4kotlin.annotation/tree/main'
                }

                licenses {
                    license {
                        name = 'MIT'
                        url = licenseURL
                    }
                }

                developers {
                    developer {
                        id = 'wickie73'
                        name = 'Wilhelm Schulenburg'
                    }
                }
            }
        }
    }
}
