apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"

ext {
    libGroupId = group
    libArtifactId = 'annotation'
    libVersion = version
    title = 'Mockito Annotations for Kotlin'
    descriptionPOM = 'Mockito Annotations for Kotlin: a small Kotlin library which supports Annotations for Mockito, Mockito-Kotlin or Mockito4k.'
    licenseURL = 'https://opensource.org/licenses/MIT'
}

ext.sourceManifest = manifest {
    attributes(
        "Bundle-Name": title + " Sources",
        "Bundle-SymbolicName": project.name + ".source",
        "Bundle-Version": version,
        "Bundle-ManifestVersion": "2",
        "Bundle-ActivationPolicy": "lazy",
        "Bundle-License": licenseURL,
        "Created-By": "wickie73",
        "Eclipse-SourceBundle": project.name + ";version=" + '"' + version + '"'
    )
}

jar.manifest {
    attributes(
        "Bundle-Name": title,
        "Bundle-SymbolicName": project.name,
        "Bundle-Version": version,
        "Bundle-RequiredExecutionEnvironment": "JavaSE-1.8",
        "Bundle-ManifestVersion": "2",
        "Bundle-ActivationPolicy": "lazy",
        "Bundle-License": licenseURL,
        "Implementation-Title": title,
        "Implementation-Version": version,
        "Created-By": "wickie73",
        "Specification-Title": title,
        "Specification-Version": version,
        "Specification-Vendor": "wickie73",
        "Export-Package": "org.mockito4kotlin.annotation",
        "Import-Package": 'com.nhaarman.mockito_kotlin;version="1.5.0"',
        "Require-Bundle": 'org.mockito;version="[2.13,3)",org.jetbrains.kotlin;version="[1.2.20,1.3)"',
        "Build-Jdk": java_version
    )
}

dokka {
    outputFormat = "html"
    outputDirectory = "build/dokka"

    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/wickie73/mockito4kotlin.annotation/tree/master/src/main/kotlin"
        suffix = "#L"
    }

    jdkVersion = 8
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    manifest = project.manifest {
        from sourceManifest
    }
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from dokka.outputDirectory
}

javadoc.dependsOn dokka

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url licenseURL
        }
    }
    developers {
        developer {
            id "wickie73"
            name "Wilhelm Schulenburg"
        }
    }
}

publishing {
    publications {
        Production(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            groupId libGroupId
            artifactId libArtifactId
            version libVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('description', descriptionPOM)
                root.appendNode('name', title)
                root.appendNode('url', 'https://github.com/wickie73/mockito4kotlin.annotation')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty("user") ? project.property("user") : ''
    key = project.hasProperty("key") ? project.property("key") : ''
    publications = ['Production']
    publish = true
    override = false
    dryRun = false

    pkg {
        repo = "wickieMaven"
        name = project.name
        desc = descriptionPOM
        licenses = ["MIT"]
        vcsUrl = "https://github.com/wickie73/mockito4kotlin.annotation.git"
        version {
            name = libVersion
            released  = new Date()
        }
    }
}

