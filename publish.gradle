apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"

ext {
    libGroupId = 'org.mockito4kotlin'
    libArtifactId = 'annotation'
    libVersion = version
    title = 'Mockito Annotations for Kotlin'
    descriptionPOM = 'Mockito Annotations for Kotlin: a small Kotlin library which supports Annotations for Mockito, Mockito-Kotlin or Mockito4k.'
    licenseURL = 'https://opensource.org/licenses/MIT'
}

ext.sourceManifest = manifest {
    attributes(
        "Bundle-Name": title + " Sources",
        "Bundle-SymbolicName": project.name + ".source",
        "Bundle-Version": version,
        "Bundle-ManifestVersion": "2",
        "Bundle-ActivationPolicy": "lazy",
        "Bundle-License": licenseURL,
        "Created-By": "wickie73",
        "Eclipse-SourceBundle": project.name + ";version=" + '"' + version + '"'
    )
}

jar.manifest {
    attributes(
        "Bundle-Name": title,
        "Bundle-SymbolicName": project.name,
        "Bundle-Version": version,
        "Bundle-RequiredExecutionEnvironment": "JavaSE-1.8",
        "Bundle-ManifestVersion": "2",
        "Bundle-ActivationPolicy": "lazy",
        "Bundle-License": licenseURL,
        "Implementation-Title": title,
        "Implementation-Version": version,
        "Created-By": "wickie73",
        "Specification-Title": title,
        "Specification-Version": version,
        "Specification-Vendor": "wickie73",
        "Export-Package": "org.mockito4kotlin.annotation",
        "Import-Package": 'com.nhaarman.mockitokotlin2;version="[2.0.0,3)"',
        "Require-Bundle": 'org.mockito;version="[2.13,4)",org.jetbrains.kotlin;version="[1.3.0,1.5)"',
        "Build-Jdk": java_version
    )
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            displayName.set("JVM")
            jdkVersion.set(8)
            sourceLink {
                remoteUrl.set(java.net.URL("https://github.com/wickie73/mockito4kotlin.annotation/tree/master/src/main/kotlin"))
                remoteLineSuffix.set("#L")
            }
            perPackageOption {
                matchingRegex.set(".*\\.internal.*") // will match all .internal packages and sub-packages
                suppress.set(true)
            }
        }
    }
}

task sourcesJar(type: Jar) {
    manifest = project.manifest {
        from sourceManifest
    }
    from sourceSets.main.allSource
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    from dokkaHtml.outputDirectory
}

javadoc.dependsOn dokkaHtml

java {
    withJavadocJar()
    withSourcesJar()
}

def pomConfig = {
    licenses {
        license {
            name = "MIT"
            url = licenseURL
        }
    }
    developers {
        developer {
            id = "wickie73"
            name = "Wilhelm Schulenburg"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar

            groupId = libGroupId
            artifactId = libArtifactId
            version = libVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('description', descriptionPOM)
                root.appendNode('name', title)
                root.appendNode('url', 'https://github.com/wickie73/mockito4kotlin.annotation')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty("user") ? project.property("user") : ''
    key = project.hasProperty("key") ? project.property("key") : ''

    publish = false
    override = true
    dryRun = false
    publications = ['mavenJava']

    pkg {
        repo = "wickieMaven"
        name = project.name
        desc = descriptionPOM
        licenses = ["MIT"]
        websiteUrl = "https://github.com/wickie73/mockito4kotlin.annotation"
        issueTrackerUrl = "https://github.com/wickie73/mockito4kotlin.annotation/issues"
        vcsUrl = "https://github.com/wickie73/mockito4kotlin.annotation.git"
        publicDownloadNumbers = true
        githubRepo = "wickie73/mockito4kotlin.annotation"
        githubReleaseNotesFile = "README.md"
        version {
            name = libVersion
            released = new Date()
        }
    }
}
